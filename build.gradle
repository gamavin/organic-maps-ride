import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.11.0' apply false
    id 'com.android.library'     version '8.11.0' apply false
    // Tambahan ini membantu Gradle memakai satu classloader plugin Kotlin untuk semua subproject
    id 'org.jetbrains.kotlin.android'         version '2.0.21' apply false
    id 'org.jetbrains.kotlin.plugin.parcelize' version '2.0.21' apply false
}

def run(cmd) {
    def output = providers.exec {
        commandLine = cmd
    }
    return output.standardOutput.asText.get().trim()
}

def getVersion() {
    def isWindows = DefaultNativePlatform.getCurrentOperatingSystem().isWindows()
    def bash = isWindows ? 'C:\\Program Files\\Git\\bin\\bash.exe' : 'bash'
    def versionCode = Integer.parseInt(run([bash, '../tools/unix/version.sh', 'android_code']))
    def versionName = run([bash, '../tools/unix/version.sh', 'android_name'])
    return new Tuple2(versionCode, versionName)
}

rootProject.ext {
    def ver = getVersion()
    versionCode = ver.V1
    versionName = ver.V2
}
